macro(import_targets type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/openai-${type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "ApiOpenAI ${type} libraries were requested but not found")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/openai-${type}-targets.cmake")
endmacro()

if(NOT TARGET OpenAI::OpenAI)
    set(type "")

    if(DEFINED OpenAI_SHARED_LIBS)
        if(OpenAI_SHARED_LIBS)
            set(type "shared")
        else()
            set(type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/openai-shared-targets.cmake")
        set(type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/openai-static-targets.cmake")
        set(type "static")
    else()
        set(type "shared")
    endif()

    import_targets(${type})
    check_required_components(OpenAI)
    message("-- Found ${type} ApiOpenAI (version ${${CMAKE_FIND_PACKAGE_NAME}_VERSION})")
endif()